namespace = EDG_flavorMLO
normal_or_historical_nations = yes
country_event = {#意大利王副与米兰选帝大主教
  id = EDG_flavorMLO.0
  title = "EDG_flavorMLO.0.t"
  desc = "EDG_flavorMLO.0.d"
  picture = GOOD_WITH_MONARCH_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    has_ruler_flag = EDG_MLO_Filippo_FLAG
  }
  
  option = {#圣安布罗斯庇护着伦巴第。
    name = "EDG_flavorMLO.0.a"
    ai_chance = {
      factor = 10
    }
    set_country_flag = EDG_flavorMLO_0_flag
    custom_tooltip = EDG_flavorMLO_0_tt_1
    custom_tooltip = EDG_flavorMLO_0_tt_2

    custom_tooltip = EDG_SPACE_TT
    #tooltip = {
      add_ruler_modifier = {
        name = EDG_flavorMLO_0_modifier
        duration = -1
      }
      add_ruler_modifier = {
        name = EDG_flavorMLO_0_modifier_2
        duration = -1
      }
    #}
    every_country = {
      limit = { is_subject_of = ROOT }
      add_liberty_desire = 50
    }

    custom_tooltip = EDG_SPACE_TT
    add_prestige_or_monarch_power = { amount = 10 }
  }
}
country_event = {#哲人议会的决议
  id = EDG_flavorMLO.1
  title = "EDG_flavorMLO.1.t"
  desc = "EDG_flavorMLO.1.d"
  picture = GOOD_WITH_MONARCH_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    is_year = 1445
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#我们必须加强对哲人议会的控制。
    name = "EDG_flavorMLO.1.a"
    ai_chance = {
      factor = 10
    }
    custom_tooltip = EDG_flavorMLO_1_a_flag_tt
    set_country_flag = EDG_flavorMLO_1_a_flag
    remove_country_modifier = EDG_flavorMLO_0_modifier_2
    add_stability = -1
    add_country_modifier = {
      name = EDG_flavorMLO_1_modifier
      duration = 3650
    }
  }
  option = {#我们应当尊重哲人议会的权力。
    name = "EDG_flavorMLO.1.b"
    ai_chance = {
      factor = 10
    }
    custom_tooltip = EDG_flavorMLO_1_b_flag_tt
    set_country_flag = EDG_flavorMLO_1_b_flag
    add_stability = 1
    add_adm_power = 100
    add_country_modifier = {
      name = EDG_flavorMLO_1_modifier_b
      duration = 3650
    }
  }
}
country_event = {#来自皮埃蒙特的流亡者
  id = EDG_flavorMLO.2
  title = "EDG_flavorMLO.2.t"
  desc = "EDG_flavorMLO.2.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    religion = catholic
    PIT = {
      exists = yes
      religion = EDG_catharism
    }
    is_year = 1447
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#欢迎他们的到来。
    name = "EDG_flavorMLO.2.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = 25
    add_prestige_or_monarch_power = { amount = 5 }

    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_2_modifier
      duration = 3650
    }

    custom_tooltip = EDG_SPACE_TT
    PIT = {
      add_opinion = {
        who = ROOT
        modifier = opinion_EDG_angry
      }
    }
  }
  option = {#我们不应该激化同皮埃蒙特清洁派的矛盾。
    name = "EDG_flavorMLO.2.b"
    ai_chance = {
      factor = 0
    }
    add_prestige = -5
  }
}
country_event = {#来自拜占庭的流亡者
  id = EDG_flavorMLO.3
  title = "EDG_flavorMLO.3.t"
  desc = "EDG_flavorMLO.3.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    NOT = { exists = BYZ }
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#我们热切地欢迎这些东方人。
    name = "EDG_flavorMLO.3.a"
    ai_chance = {
      factor = 10
    }
    add_treasury = -50
    add_adm_power = -50
    add_dip_power = 25
    
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_3_modifier
      duration = 3650
    }
  }
  option = {#他们不过是一群流浪汉。
    name = "EDG_flavorMLO.3.b"
    ai_chance = {
      factor = 0
    }
    add_prestige = -5
  }
}
country_event = {#与教宗达成同盟
  id = EDG_flavorMLO.4
  title = "EDG_flavorMLO.4.t"
  desc = "EDG_flavorMLO.4.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    alliance_with = PAP
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#我们是最虔诚的基督徒。
    name = "EDG_flavorMLO.4.a"
    ai_chance = {
      factor = 10
    }
    add_papal_influence = 10
    add_prestige_or_monarch_power = { amount = 5 }
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_4_modifier
      duration = 3650
    }
  }
}
country_event = {#同皇帝的坚定同盟
  id = EDG_flavorMLO.5
  title = "EDG_flavorMLO.5.t"
  desc = "EDG_flavorMLO.5.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  major = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    alliance_with = emperor
    is_part_of_hre = yes
    NOT = { is_year = 1550 }
    government = monarchy
  }
  mean_time_to_happen = {
  }
  
  option = {#我们是帝国忠诚的意大利王副。
    name = "EDG_flavorMLO.5.a"
    ai_chance = {
      factor = 10
    }
    add_imperial_influence = 5
    add_prestige_or_monarch_power = { amount = 5 }
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_5_modifier
      duration = 3650
    }
  }
}
country_event = {#伦巴第-法兰西同盟
  id = EDG_flavorMLO.6
  title = "EDG_flavorMLO.6.t"
  desc = "EDG_flavorMLO.6.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    alliance_with = FRA
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#法国人是我们对抗帝国的天然盟友。
    name = "EDG_flavorMLO.6.a"
    ai_chance = {
      factor = 10
    }
    add_prestige_or_monarch_power = { amount = 5 }
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_6_modifier
      duration = 3650
    }
  }
}
country_event = {#伦巴第-保加利亚同盟
  id = EDG_flavorMLO.7
  title = "EDG_flavorMLO.7.t"
  desc = "EDG_flavorMLO.7.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    OR = {
      alliance_with = BUL
      alliance_with = BU1
    }
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#我们必须同保加利亚人的沙皇保持良好的关系。
    name = "EDG_flavorMLO.7.a"
    ai_chance = {
      factor = 10
    }
    add_prestige_or_monarch_power = { amount = 5 }
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_7_modifier
      duration = 3650
    }
  }
}
country_event = {#伦巴第-阿斯图里亚斯同盟
  id = EDG_flavorMLO.8
  title = "EDG_flavorMLO.8.t"
  desc = "EDG_flavorMLO.8.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    
    OR = {
      tag = MLO
      was_tag = MLO
    }
    alliance_with = ASU
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {
  }
  
  option = {#我们是支持收复失地运动的重要力量。
    name = "EDG_flavorMLO.8.a"
    ai_chance = {
      factor = 10
    }
    add_prestige_or_monarch_power = { amount = 5 }
    custom_tooltip = EDG_SPACE_TT
    add_country_modifier = {
      name = EDG_flavorMLO_8_modifier
      duration = 3650
    }
  }
}
country_event = {#修缮米兰主座教堂
  id = EDG_flavorMLO.10
  title = "EDG_flavorMLO.10.t"
  desc = "EDG_flavorMLO.10.d"
  picture = RELIGIOUS_TURMOIL_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    owns = 104
    religion = catholic
    is_year = 1447
    NOT = { is_year = 1550 }
  }
  mean_time_to_happen = {

  }
  immediate = {
    set_country_flag = EDG_flavorMLO_10_flag
  }
  
  option = {#她是我们虔诚的象征。
    name = "EDG_flavorMLO.10.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = -100
    add_treasury = -378.76
    104 = {
      add_great_project_tier = {
      	type = duomo_milano
      	tier = 2
      }
    }
  }
  option = {#算了吧。
    name = "EDG_flavorMLO.10.b"
    ai_chance = {
      factor = 10
    }
    add_prestige = -5
  }
}
country_event = {#哲人王选举
  id = EDG_flavorMLO.11
  title = "EDG_flavorMLO.11.t"
  desc = "EDG_flavorMLO.11.d"
  picture = WESTERNISATION_eventPicture
  is_triggered_only = yes
  trigger = {
    has_government_attribute = EDG_Philosopher_reform_ability
    government = monarchy
    has_heir = no
    NOT = { has_reform = EDG_Lombardy_Philosopher_Council }
    has_consort = yes
    ruler_age = 35
  }
  mean_time_to_happen = {

  }
  
  option = {#哲学水平最高的儿子将会成为我们的继承人。
    name = "EDG_flavorMLO.11.f"
    ai_chance = {
      factor = 100
    }
    if = {
      limit = {
        NOT = { ruler_age = 35 }
      }
      define_heir = {
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
      }
    }
    else = {
      define_heir = {
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
        age = 15
      }
    }
    
    custom_tooltip = EDG_SPACE_TT
    if = {
      limit = { has_country_flag = EDG_MISSION_MLO_heir_adm_flag }
      custom_tooltip = EDG_MLO_change_heir_adm_TT
      hidden_effect = { change_heir_adm = 2 }
    }
    if = {
      limit = { has_country_flag = EDG_MISSION_MLO_heir_dip_flag }
      custom_tooltip = EDG_MLO_change_heir_dip_TT
      hidden_effect = { change_heir_dip = 2 }
    }
    if = {
      limit = { has_country_flag = EDG_MISSION_MLO_heir_mil_flag }
      custom_tooltip = EDG_MLO_change_heir_mil_TT
      hidden_effect = { change_heir_mil = 2 }
    }
  }
  option = {#最强壮的儿子将会是我们的继承人。
    name = "EDG_flavorMLO.11.a"
    ai_chance = {
      factor = 100
    }
    if = {
      limit = {
        NOT = { ruler_age = 35 }
      }
      define_heir = {
        change_mil = 2
        max_random_adm = 4
        max_random_dip = 4
        max_random_mil = 6
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
      }
    }
    else = {
      define_heir = {
        change_mil = 2
        max_random_adm = 4
        max_random_dip = 4
        max_random_mil = 6
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
        age = 15
      }
    }
  }
  option = {#最博学的儿子将会是我们的继承人。
    name = "EDG_flavorMLO.11.b"
    ai_chance = {
      factor = 100
    }
    if = {
      limit = {
        NOT = { ruler_age = 35 }
      }
      define_heir = {
        change_dip = 2
        max_random_adm = 4
        max_random_dip = 6
        max_random_mil = 4
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
      }
    }
    else = {
      define_heir = {
        change_dip = 2
        max_random_adm = 4
        max_random_dip = 6
        max_random_mil = 4
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
        age = 15
      }
    }
  }
  option = {#最机敏的儿子将会是我们的继承人。
    name = "EDG_flavorMLO.11.c"
    ai_chance = {
      factor = 100
    }
    if = {
      limit = {
        NOT = { ruler_age = 35 }
      }
      define_heir = {
        change_adm = 2
        max_random_adm = 6
        max_random_dip = 4
        max_random_mil = 4
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
      }
    }
    else = {
      define_heir = {
        change_adm = 2
        max_random_adm = 6
        max_random_dip = 4
        max_random_mil = 4
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
        age = 15
      }
    }
  }
  option = {#最忠诚的儿子将会是我们的继承人。
    name = "EDG_flavorMLO.11.e"
    ai_chance = {
      factor = 100
    }
    if = {
      limit = {
        NOT = { adm = 6 }
      }
      change_adm = 1
    }
    else_if = {
      limit = {
        NOT = { dip = 6 }
      }
      change_dip = 1
    }
    else_if = {
      limit = {
        NOT = { mil = 6 }
      }
      change_mil = 1
    }
    if = {
      limit = {
        NOT = { ruler_age = 45 }
      }
      define_heir = {
        max_random_adm = 5
        max_random_dip = 5
        max_random_mil = 5
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
      }
    }
    else = {
      define_heir = {
        max_random_adm = 5
        max_random_dip = 5
        max_random_mil = 5
        claim = 100
        culture = ROOT
        religion = ROOT
        dynasty = ROOT
        age = 15
      }
    }
  }
}

country_event = {#斯福尔扎财政改革
  id = EDG_flavorMLO.12
  title = "EDG_flavorMLO.12.t"
  desc = "EDG_flavorMLO.12.d"
  picture = ADVISOR_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    is_year = 1455
  }
  mean_time_to_happen = {

  }
  immediate = {
    
  }
  
  option = {#财政制度是时候改善了！
    name = "EDG_flavorMLO.12.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = -50
    add_treasury = 378.76
    add_country_modifier = {
      name = EDG_flavorMLO_12_modifier
      duration = 7300
    }
  }
  option = {#将资源投入到别处。
    name = "EDG_flavorMLO.12.b"
    ai_chance = {
      factor = 2
    }
    add_adm_power = 100
  }
}
country_event = {#伦巴第的大陆均势政策
  id = EDG_flavorMLO.13
  title = "EDG_flavorMLO.13.t"
  desc = "EDG_flavorMLO.13.d"
  picture = ITALIAN_wars_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    is_year = 1480
  }
  mean_time_to_happen = {

  }
  immediate = {
    
  }
  
  option = {#我们是智慧的八爪蜘蛛。
    name = "EDG_flavorMLO.13.a"
    ai_chance = {
      factor = 10
    }
    add_country_modifier = {
      name = EDG_flavorMLO_13_modifier
      duration = 7300
    }
  }
  option = {#哈哈。
    name = "EDG_flavorMLO.13.b"
    ai_chance = {
      factor = 2
    }
    add_prestige_or_monarch_power = { amount = 5 }
    add_dip_power = 25
  }
}
country_event = {#伦巴第-佛罗伦萨战争
  id = EDG_flavorMLO.14
  title = "EDG_flavorMLO.14.t"
  desc = "EDG_flavorMLO.14.d"
  picture = ITALIAN_wars_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    any_province = {
      is_capital_of = LAN
      controlled_by = ROOT
    }
  }
  immediate = {
    
  }
  mean_time_to_happen = {

  }
  
  option = {#把所有艺术品通通搬走！
    name = "EDG_flavorMLO.14.a"
    ai_chance = {
      factor = 10
    }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 3
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 3
      }
    }

    custom_tooltip = EDG_SPACE_TT
    add_treasury = 100
    add_adm_power = 50
    add_dip_power = 50
    add_mil_power = 50

    custom_tooltip = EDG_SPACE_TT
    random_province = {
      limit = { is_capital_of = LAN controlled_by = ROOT }
      add_devastation = 50
      if = { limit = { base_tax = 2 } add_base_tax = -1 }
      if = { limit = { base_production = 2 } add_base_production = -1 }
      if = { limit = { base_manpower = 2 } add_base_manpower = -1 }
    }

    custom_tooltip = EDG_SPACE_TT
    every_country = {
      limit = { religion = catholic culture_group = latin }
      add_opinion = {
        who = ROOT
        modifier = po_EDG_massacre_latin_opinion
      }
    }
  }
  option = {#我们不应该这么做。
    name = "EDG_flavorMLO.14.b"
    ai_chance = {
      factor = 2
    }
    random_province = {
      limit = {
        is_capital_of = LAN
        controlled_by = ROOT
      }
      add_devastation = 10
    }
  }
}
country_event = {#宗座的宝库
  id = EDG_flavorMLO.15
  title = "EDG_flavorMLO.15.t"
  desc = "EDG_flavorMLO.15.d"
  picture = IMPORTANT_STATUE_eventPicture
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    any_province = {
      is_capital_of = PAP
      controlled_by = ROOT
    }
  }
  fire_only_once = yes
  is_triggered_only = yes
  immediate = {
    
  }
  mean_time_to_happen = {

  }
  
  option = {#我们很早就想劫掠圣彼得大教堂的艺术品了。
    name = "EDG_flavorMLO.15.a"
    ai_chance = {
      factor = 10
    }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 5
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 5
      }
    }

    custom_tooltip = EDG_SPACE_TT
    add_treasury = 200
    add_adm_power = 100
    add_dip_power = 100
    add_mil_power = 100

    custom_tooltip = EDG_SPACE_TT
    random_province = {
      limit = { is_capital_of = PAP controlled_by = ROOT }
      add_devastation = 50
      if = { limit = { base_tax = 2 } add_base_tax = -1 }
      if = { limit = { base_production = 2 } add_base_production = -1 }
      if = { limit = { base_manpower = 2 } add_base_manpower = -1 }
    }

    custom_tooltip = EDG_SPACE_TT
    every_country = {
      limit = { religion = catholic culture_group = latin }
      add_opinion = {
        who = ROOT
        modifier = po_EDG_massacre_latin_opinion
      }
    }
  }
  option = {#我们不应该这么做。
    name = "EDG_flavorMLO.15.b"
    ai_chance = {
      factor = 2
    }
    random_province = {
      limit = {
        is_capital_of = PAP
        controlled_by = ROOT
      }
      add_devastation = 10
    }
  }
}
country_event = {#威尼斯总督宫的艺术品
  id = EDG_flavorMLO.16
  title = "EDG_flavorMLO.16.t"
  desc = "EDG_flavorMLO.16.d"
  fire_only_once = yes
  is_triggered_only = yes
  picture = IMPORTANT_STATUE_eventPicture
  trigger = {
    OR = {
      tag = MLO
      was_tag = MLO
    }
    any_province = {
      is_capital_of = VEN
      controlled_by = ROOT
    }
  }
  mean_time_to_happen = {

  }

  immediate = {
    
  }
  
  option = {#威尼斯总督宫里一定有很多艺术品。
    name = "EDG_flavorMLO.16.a"
    ai_chance = {
      factor = 10
    }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 2
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 2
      }
    }

    custom_tooltip = EDG_SPACE_TT
    add_treasury = 200
    add_adm_power = 100
    add_dip_power = 100
    add_mil_power = 100

    custom_tooltip = EDG_SPACE_TT
    random_province = {
      limit = { is_capital_of = VEN controlled_by = ROOT }
      add_devastation = 50
      if = { limit = { base_tax = 2 } add_base_tax = -1 }
      if = { limit = { base_production = 2 } add_base_production = -1 }
      if = { limit = { base_manpower = 2 } add_base_manpower = -1 }
    }

    custom_tooltip = EDG_SPACE_TT
    every_country = {
      limit = { religion = catholic culture_group = latin }
      add_opinion = {
        who = ROOT
        modifier = po_EDG_massacre_latin_opinion
      }
    }
  }
  option = {#我们不应该这么做。
    name = "EDG_flavorMLO.15.b"
    ai_chance = {
      factor = 2
    }
    random_province = {
      limit = {
        is_capital_of = VEN
        controlled_by = ROOT
      }
      add_devastation = 10
    }
  }
}
country_event = {#《最后的晚餐》
  id = EDG_flavorMLO.17
  title = "EDG_flavorMLO.17.t"
  desc = "EDG_flavorMLO.17.d"
  picture = ADVISOR_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = ITA
      tag = MLO
      was_tag = MLO
    }
    is_year = 1494
    NOT = { is_year = 1510 }
  }
  mean_time_to_happen = {

  }
  immediate = {
    
  }
  
  option = {#完美！
    name = "EDG_flavorMLO.17.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = 75
    add_prestige_or_monarch_power = { amount = 10 }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 1
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 1
      }
    }
    add_government_power = {
      mechanic_type = EDG_MLO_government_ability
      power_type = EDG_MLO_government_power
      value = 5
    }
  }
}
country_event = {#《岩间圣母》
  id = EDG_flavorMLO.18
  title = "EDG_flavorMLO.18.t"
  desc = "EDG_flavorMLO.18.d"
  picture = IMPORTANT_STATUE_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    OR = {
      tag = ITA
      tag = MLO
      was_tag = MLO
    }
    is_year = 1485
    NOT = { is_year = 1510 }
  }
  mean_time_to_happen = {

  }
  immediate = {
    
  }
  
  option = {#很好！
    name = "EDG_flavorMLO.18.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = 75
    add_prestige_or_monarch_power = { amount = 10 }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 1
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 1
      }
    }
    add_government_power = {
      mechanic_type = EDG_MLO_government_ability
      power_type = EDG_MLO_government_power
      value = 5
    }
  }
}
country_event = {#《圣母子与圣安妮》
  id = EDG_flavorMLO.19
  title = "EDG_flavorMLO.19.t"
  desc = "EDG_flavorMLO.19.d"
  picture = IMPORTANT_STATUE_eventPicture
  fire_only_once = yes
  is_triggered_only = yes
  trigger = {
    has_institution = renaissance
    is_year = 1505
    NOT = { is_year = 1510 }
    owns = 116
  }
  mean_time_to_happen = {

  }
  immediate = {
    
  }
  
  option = {#令人震撼。
    name = "EDG_flavorMLO.19.a"
    ai_chance = {
      factor = 10
    }
    add_adm_power = 75
    add_prestige_or_monarch_power = { amount = 10 }
    change_variable = {
      which = EDG_Purchase_artwork_value_ITA
      value = 1
    }
    hidden_effect = {
      change_variable = {
        which = EDG_Purchase_artwork_value
        value = 1
      }
    }
    add_government_power = {
      mechanic_type = EDG_MLO_government_ability
      power_type = EDG_MLO_government_power
      value = 5
    }
  }
}