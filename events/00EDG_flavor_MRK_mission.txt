namespace = EDG_flavorMRK_mission
normal_or_historical_nations = yes
country_event = {#图阿雷格人的地位
  id = EDG_flavorMRK_mission.1
  title = "EDG_flavorMRK_mission.1.t"
  desc = "EDG_flavorMRK_mission.1.d"
  picture = DIPLOMACY_eventPicture
  is_triggered_only = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#他们应当更彻底地重归柏柏尔的怀抱。
    name = "EDG_flavorMRK_mission.1.a"
    ai_chance = {
      factor = 10
    }
    set_country_flag = EDG_turn_tuareg_into_berber_continuously_flag
    custom_tooltip = EDG_turn_tuareg_into_berber_continuously_flag_tt
  }
  option = {#他们是我们的兄弟。
    name = "EDG_flavorMRK_mission.1.b"
    ai_chance = {
      factor = 10
    }
    tooltip = {
			add_accepted_culture_or_dip_power = { culture = tuareg }
    }
    add_country_modifier = {
      name = EDG_flavorMRK_mission_1_b_modifier
      duration = -1
    }
    hidden_effect = {
			add_accepted_culture_or_dip_power = { culture = tuareg }
    }
  }
}
country_event = {#[From.GetName]的提议
  id = EDG_flavorMRK_mission.2
  title = "EDG_flavorMRK_mission.2.t"
  desc = "EDG_flavorMRK_mission.2.d"
  picture = DIPLOMACY_eventPicture
  is_triggered_only = yes
  major = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#他们的权威如沙漠中的太阳般耀眼。
    name = "EDG_flavorMRK_mission.2.a"
    ai_chance = {
      factor = 10
    }
    FROM = {
      create_subject = { subject_type = vassal subject = ROOT }
      add_opinion = {
        who = ROOT
        modifier = opinion_EDG_HAB_mission_1_2_a
      }
    }
  }
  option = {#他们迟早会招来安拉的怒火。
    name = "EDG_flavorMRK_mission.2.b"
    ai_chance = {
      factor = 0
    }
    FROM = {
      add_casus_belli = {
        target = ROOT
        type = cb_annex
        months = 600
      }
      add_opinion = {
        who = ROOT
        modifier = opinion_EDG_HAB_mission_1_2_b
      }
    
    }
  }
}
country_event = {#文化认同
  id = EDG_flavorMRK_mission.3
  title = "EDG_flavorMRK_mission.3.t"
  desc = "EDG_flavorMRK_mission.3.d"
  picture = DIPLOMACY_eventPicture
  is_triggered_only = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#我们愿意接受先进的摩洛哥文化。
    name = "EDG_flavorMRK_mission.3.a"
    ai_chance = {
      factor = 10
    }
    change_culture = moroccan
    add_accepted_culture = berber
    custom_tooltip = EDG_flavorMRK_mission_3_a_flag_tt_
  }
  option = {#我们将维持柏柏尔传统。
    name = "EDG_flavorMRK_mission.3.b"
    ai_chance = {
      factor = 0
    }
    set_country_flag = EDG_flavorMRK_mission_3_b_flag
    custom_tooltip = EDG_flavorMRK_mission_3_b_flag_tt_
  }
}
country_event = {#穆瓦希德派
  id = EDG_flavorMRK_mission.4
  title = "EDG_flavorMRK_mission.4.t"
  desc = "EDG_flavorMRK_mission.4.d"
  picture = RELIGIOUS_WARS_eventPicture
  is_triggered_only = yes
  major = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#我们的马赫迪，真主的利刃。
    name = "EDG_flavorMRK_mission.4.a"
    ai_chance = {
      factor = 10
    }
    tooltip = {
      change_religion = EDG_Muwashid
    }
    change_variable = {
      which = EDG_MRK_Mahdi_value
      value = 1
    }
    custom_tooltip = EDG_flavorMRK_mission_4_a_tt
    hidden_effect = {
      add_prestige_or_monarch_power = { amount = 100 }
    }
    custom_tooltip = EDG_flavorMRK_mission_4_a_tt_change_religion
    hidden_effect = {
      every_country = {
        limit = {
          OR = {
            tag = ROOT
            is_subject_of = ROOT
          }
          religion = ROOT
        }
        every_owned_province = {
          limit = {
            religion = ROOT
          }
          change_religion = EDG_Muwashid
        }
        change_religion = EDG_Muwashid
      }
    }
  }
  option = {#哈里发应当维护和平。
    name = "EDG_flavorMRK_mission.4.b"
    ai_chance = {
      factor = 0
    }
    custom_tooltip = EDG_flavorMRK_mission_4_b_tt
  }
}
country_event = {#马赫迪、哈里发与沙汗沙阿
  id = EDG_flavorMRK_mission.5
  title = "EDG_flavorMRK_mission.5.t"
  desc = "EDG_flavorMRK_mission.5.d"
  picture = RELIGIOUS_WARS_eventPicture
  is_triggered_only = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#向至高无上的马赫迪、哈里发与沙汗沙阿叩首。
    name = "EDG_flavorMRK_mission.5.a"
    ai_chance = {
      factor = 10
    }
    add_prestige_or_monarch_power = { amount = 20 }
    set_country_flag = unified_islam
    set_global_flag = unified_islam_global
    add_country_modifier = {
      name = unified_islam
      duration = -1
    }
  }
  option = {#我们是唯一的正统哈里发。
    name = "EDG_flavorMRK_mission.5.b"
    ai_chance = {
      factor = 10
    }
    if = {
      limit = {
        NOT = {
          government_rank = 3
        }
      }
      set_government_rank = 3
    }
    override_country_name = THE_CALIPHATE
    set_country_flag = unified_islam
    set_global_flag = unified_islam_global
    unlock_government_reform = {
      government_reform = caliphate_reform
    }
    if = {
      limit = {
        OR = {
          government = monarchy
          government = theocracy
        }
        NOT = { has_reform = ottoman_government }
        NOT = { has_reform = modernized_ottoman_government }
        NOT = { has_reform = reorganized_ottoman_government }
        NOT = { has_reform = caliphate_reform }
        NOT = { has_reform = caliphate_theocracy_reform }
      }
      if = {
        limit = {
          government = monarchy
        }
        add_government_reform = caliphate_reform
      }
      if = {
        limit = {
          government = theocracy
        }
        add_government_reform = caliphate_theocracy_reform
      }
    }
    add_country_modifier = {
      name = unified_islam
      duration = -1
    }
  }
}
country_event = {#劫掠君士坦丁堡
  id = EDG_flavorMRK_mission.6
  title = "EDG_flavorMRK_mission.6.t"
  desc = "EDG_flavorMRK_mission.6.d"
  picture = FORT_eventPicture
  is_triggered_only = yes
  trigger = {
  }
  mean_time_to_happen = {
  }
  
  option = {#我们的马赫迪，真主的利刃。
    name = "EDG_flavorMRK_mission.6.a"
    ai_chance = {
      factor = 10
    }
    add_treasury = 1000
    EDG_MRE_power_change_small_effect = yes
    151 = {
      add_devastation = 50
      change_religion = EDG_Muwashid
      EDG_exploit_province_development_Five_times_effect = yes
    }
  }
  option = {#哈里发应当维护和平。
    name = "EDG_flavorMRK_mission.6.b"
    ai_chance = {
      factor = 0
    }
    add_prestige_or_monarch_power = { amount = 5 }
  }
}
country_event = {#柏柏尔化
  id = EDG_flavorMRK_mission.7
  title = "EDG_flavorMRK_mission.7.t"
  desc = "EDG_flavorMRK_mission.7.d"
  picture = JESUTIS_IN_BRAZIL_eventPicture
  
  trigger = {
    has_country_flag = EDG_flavorMRK_mission_3_b_flag
    num_of_owned_provinces_with = {
      value = 1
      culture_group = maghrebi
      NOT = { culture = berber }
    }
    NOT = { has_country_flag = EDG_flavorMRK_mission_7_b_tt_FLAG }
  }
  mean_time_to_happen = {
    months = 36
    modifier = {
      factor = 0.25
      has_country_flag = EDG_MRK_civil_war_1_event_2_flag
    }
  }
  
  option = {#很好。
    name = "EDG_flavorMRK_mission.7.a"
    ai_chance = {
      factor = 10
    }
    random_owned_province = {
      limit = {
        culture_group = maghrebi
        NOT = { culture = berber }
      }
      change_culture = berber
    }
  }
  option = {#我希望停止这种倾向。
    name = "EDG_flavorMRK_mission.7.b"
    ai_chance = {
      factor = 0
    }
    set_country_flag = EDG_flavorMRK_mission_7_b_tt_FLAG
    custom_tooltip = EDG_flavorMRK_mission_7_b_tt
  }
}